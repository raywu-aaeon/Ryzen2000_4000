/*****************************************************************************
 *
 * Copyright (C) 2012-2022 Advanced Micro Devices, Inc. All rights reserved.
 *
 ******************************************************************************
 */

Method (_ADR) {
    Store(CpmReadTable(CpmMainTable, CpmZeroPowerOddEn), Local0)
    Store(CpmReadTable(CpmZeroPowerOddTable, CpmZeroPowerOddSataPortId), Local4)
    // If CpmIsWin8orAbove and ZeroPowerOdd enable
    If (And(Local0, 1)) {
        // New AHCI addressing scheme
        Return(Add(Multiply(Local4, 0x10000), 0x0000FFFF))
    } Else {
        Return(Local4)
    }
}

// Status of ODD: 1: ON. 0: OFF
Name(CpmOddStatus, 1)
Name(ODPS, 0)
// ODD boot up Flag
Name(ODDB, 0)
Name(WDSW, 0x01)

Method(_INI) {
    Store(CpmReadTable(CpmZeroPowerOddTable, CpmZeroPowerOddSataPortId), Local4)
    If (CpmIsWin8()) {
        If (LNotEqual(PBAA(), 0)) {
            If (LNotEqual(CpmReadMem32(PBAA(), Add(0x129, Multiply(Local4, 0x80)), 0, 4), 0)) {
                Store(0x1, \_SB.PCI0.GP18.SATA.ODD8.ODDB)
            }
        }
        Store(0x1, \_SB.PCI0.GP18.SATA.SACB)
    }
}

Method(ZSTA, 0) {
    Store(CpmReadTable(CpmMainTable, CpmZeroPowerOddEn), Local0)
    Store(CpmReadTable(CpmZeroPowerOddTable, CpmZeroPowerOddDeviceId), Local1)
    Store(CpmReadTable(CpmZeroPowerOddTable, CpmZeroPowerOddEventPin2), Local3)
    Store(CpmReadTable(CpmZeroPowerOddTable, CpmZeroPowerOddEventSource2), Local6)
    // ODD Zero Power is enabled
    If (And(Local0, 1)) {
        // ODD Hotplug is enabled
        If (And(Local0, 2)) {
            // Source of ODD_PLUGIN# - 0: FCH GPIO/GEVENT Pin
            If (LEqual(Local6, 0)) {
                If (CpmReadGevent(Local3)) {
                    Return(0x0)
                } Else {
                    If (And(Local0, 4)) {
                        // pretend that _PS0 is called if boot without PS0
                        Store(0x1, \_SB.PCI0.GP18.SATA.ODD8.ODDB)
                    }
                    Return(0xF)
                }
            // Source of ODD_PLUGIN# - 1: KBC GPIO/QEVENT Pin, 2: ECRAM GPIO/QEVENT Pin
            } Else {
                If (LEqual(\_SB.PCI0.LPC0.EC0.OKEC, 0x01)) {
                    If (CpmReadGpio(Add(Local3, ShiftLeft(Local6, 8)))) {
                        If (LEqual(\_SB.PCI0.GP18.SATA.SACB, 0x1)) {
                            Store(0x0, \_SB.PCI0.GP18.SATA.SACB)
                            \_SB.PCI0.GP18.SATA.STPP(1)
                            Sleep(40)
                            CpmSetDevicePower(Local1, 1)
                            Sleep(40)
                            \_SB.PCI0.LPC0.EC0.XQ41()
                        }
                        Return(0x0)
                    } Else {
                        Store(0x0, \_SB.PCI0.GP18.SATA.SACB)
                        If (And(Local0, 4)) {
                            // pretend that _PS0 is called if boot without PS0
                            Store(0x1, \_SB.PCI0.GP18.SATA.ODD8.ODDB)
                        }
                        Return(0xF)
                    }
                } Else {
                    If (And(Local0, 4)) {
                        // pretend that _PS0 is called if boot without PS0
                        Store(0x1, \_SB.PCI0.GP18.SATA.ODD8.ODDB)
                    }
                    Return(0xF)
                }
            }
        } Else {
            If (And(Local0, 4)) {
                // pretend that _PS0 is called if boot without PS0
                Store(0x1, \_SB.PCI0.GP18.SATA.ODD8.ODDB)
            }
            Return(0xF)
        }
    // ODD Zero Power disabled
    } Else {
        Return(0x0)
    }
} // End of Method(ZSTA, 0)

Method(_PSC) {
    If (CpmIsWin8()) {
        If (\_SB.PCI0.GP18.SATA.ODD8.CpmOddStatus) {
            Return(0)
        } Else {
            Return(3)
        }
    } Else {
        Return(3)
    }
} // End of Method(_PSC)

Method(_PS0) {
    Store(CpmReadTable(CpmMainTable, CpmZeroPowerOddEn), Local0)
    Store(CpmReadTable(CpmZeroPowerOddTable, CpmZeroPowerOddDeviceId), Local1)
    Store(CpmReadTable(CpmZeroPowerOddTable, CpmZeroPowerOddEventPin2), Local3)
    Store(CpmReadTable(CpmZeroPowerOddTable, CpmZeroPowerOddEventSource2), Local6)
    If (CpmIsWin8()) {
        // Source of ODD_PLUGIN# - 0: FCH GPIO/GEVENT Pin
        If (LEqual(Local6, 0)) {
            Store(CpmReadGevent (Local3), Local7)
            // Source of ODD_PLUGIN# - 1: KBC GPIO/QEVENT Pin, 2: ECRAM GPIO/QEVENT Pin
        } Else {
            If (LEqual(\_SB.PCI0.LPC0.EC0.OKEC, 0x01)) {
                Store(CpmReadGpio (Add (Local3, ShiftLeft (Local6, 8))), Local7)
            } Else {
                Store(0, Local7)
            }
        }
        If (LNot(LAnd(And(Local0, 2), Local7))) {
            CpmSetPostCode(CpmTpZeroPowerOdd_PS0)
            If (And(Local0, 1)) {
                // Second or latter time call _PS0, or First time call and ODD connected
                If (LOr(LEqual(\_SB.PCI0.GP18.SATA.ODD8.ODDB, 0x1), LAnd(LEqual(\_SB.PCI0.GP18.SATA.ODD8.ODDB, 0x0), LNotEqual(\_SB.PCI0.GP18.SATA.STPM, 0x0)))) {
                    // Enable Port
                    \_SB.PCI0.GP18.SATA.STPT(1)
                    Sleep(40)
                    CpmSetDevicePower(Local1, 1)
                    Store(1, \_SB.PCI0.GP18.SATA.ODD8.CpmOddStatus)
                    Sleep(40)
                }
            }
            // _PS0 is called
            Store(0x1, \_SB.PCI0.GP18.SATA.ODD8.ODDB)
            Store(0x0, \_SB.PCI0.GP18.SATA.ODD8.ODPS)
        }
    }
} // End of Method(_PS0)

Method(_PS3) {
    Store(CpmReadTable(CpmMainTable, CpmZeroPowerOddEn), Local0)
    Store(CpmReadTable(CpmZeroPowerOddTable, CpmZeroPowerOddDeviceId), Local1)
    If (CpmIsWin8()) {
        CpmSetPostCode(CpmTpZeroPowerOdd_PS3)
        If (And(Local0, 1)) {
            If (LEqual(\_SB.PCI0.GP18.SATA.ODD8.WDSW, 0x01)) {
                Sleep(40)
                // Power Off ODD
                CpmSetDevicePower(Local1, 0)
                Sleep(40)
                Store(0, \_SB.PCI0.GP18.SATA.ODD8.CpmOddStatus)
                // Disable Port
                \_SB.PCI0.GP18.SATA.STPT(0)
                Sleep(40)
            }
        }
        // _PS0 needs to be called
        Store(0x1, \_SB.PCI0.GP18.SATA.ODD8.ODDB)
        Store(0x3, \_SB.PCI0.GP18.SATA.ODD8.ODPS)
    }
} // End of Method(_PS3)

Method(XEJ0, 1) {
    If (CpmIsWin8()) {
        \_SB.PCI0.GP18.SATA.ODD8._PS3()
        \_SB.PCI0.GP18.SATA.STPP(0)
    }
} // End of Method(XEJ0)

Device(ODD) {
    Name(_ADR, 0x00000000)
    // Set Device Removable icon for MS hot-fix KB950186 Vista 
    Method(_RMV, 0) {
        Return(0x01)
    }

    Method(_EJ0, 1) {
        Store(CpmReadTable(CpmZeroPowerOddTable, CpmZeroPowerOddDeviceId), Local1)
        If (CpmIsWin8()) {
            CpmSetDevicePower(Local1, 0)
            Store(0, \_SB.PCI0.GP18.SATA.ODD8.CpmOddStatus)
        }
    }
} // End of Device(ODD)

Method(_STA, 0) {
    If (CpmIsWin8()) {
        Return(\_SB.PCI0.GP18.SATA.ODD8.ZSTA())
    } Else {
        Return(0)
    }
}

Name(_S0W, 0x04)

Method(_DSW, 3, Serialized) {
    If (CpmIsWin8()) {
        If (Arg0) {
            // enable device resume, this imply OS want the device to idle, instead of _DIS.
            // enable powerdown-allow-flag.
            Store(0x01, \_SB.PCI0.GP18.SATA.ODD8.WDSW)
        } Else {
            // disable powerdown-allow-flag.
            Store(0x00, \_SB.PCI0.GP18.SATA.ODD8.WDSW)
        }
    }
}

// GPE pin 6, wake from S3
Name(ZPRW, Package(){0x06, 0x3})

Method(XPRW,0) {
    If (LNot(CpmIsWin8())) {
        Store(CpmReadTable(CpmZeroPowerOddTable, CpmZeroPowerOddEventPin3), Index (\_SB.PCI0.GP18.SATA.ODD8.ZPRW, 0x00))
    }
    Return(\_SB.PCI0.GP18.SATA.ODD8.ZPRW)
}

